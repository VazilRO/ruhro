//===== eAthena Script ============================================
//= King of Emperium Hill
//===== By: =======================================================
//= AnnieRuru
//===== Current Version: ==========================================
//= 1.0
//===== Compatible With: ==========================================
//= hercules 2014-02-18
//===== Description: ==============================================
//= defends the emperium in the middle of the map until times up
//===== Topic =====================================================
//= http://hercules.ws/board/topic/4495-gvg-king-of-emperium-hill/
//===== Additional Comments: ======================================
//= Finally there is a topic for this !
//=================================================================

-	script	KoE	-1,{
	OnInit:
		disablenpc "The King#KoE";
		disablenpc "Exit#KoE";
		bindatcmd "koe", strnpcinfo(0)+"::OnCommand", 11,11;
		end;
	OnCommand:
		if ( compare( .@atcmd_parameters$, "on" ) ) goto L_start;
		else if ( compare( .@atcmd_parameters$, "off" ) ) goto L_end;
		else {
			dispbottom "type - '@koe on' to start the event";
			dispbottom "type - '@koe off' to end the event";
		}
		end;
	OnClock1900: // everyday 7pm starts
	L_start:
		if ( .start ) end;
		gvgon "guild_vs1";
		announce "The King of Emperium Hill has begun!", bc_all;
		.start = 1;
		enablenpc "The King#KoE";
		disablenpc "Exit#KoE";
		$koegid = 0;
		donpcevent "::OnRevKoE";
		maprespawnguildid "guild_vs1", $koegid, 7;
		monster "guild_vs1",49,49,"EMPERIUM",3500,1,"KoE::OnEmpDead";
		setnpctimer 0;
		initnpctimer;
		end;
	OnTimer1800000: // Last 30 minutes
	L_end:
		gvgoff "guild_vs1";
		announce "The King of Emperium Hill is over!", bc_all;
		.start = 0;
		enablenpc "Exit#KoE";
		disablenpc "The King#KoE";
		killmonsterall "guild_vs1";
		stopnpctimer;
		maprespawnguildid "guild_vs1", $koegid, 6; // uncomment this line to kick non-owner off the map when event ends
		end;
	OnEmpDead:
		$koegid = getcharid(2);
		announce "The current King of Emperium Hill is the ["+ strcharinfo(2) +"] guild.", bc_all;
		donpcevent "::OnRevKoE";
		maprespawnguildid "guild_vs1", $koegid, 6;
		sleep 500;
		if ( .start )
			monster "guild_vs1",49,49,"EMPERIUM",3500,1,"KoE::OnEmpDead";
		end;
}

// KoE Runes
//http://rathena.org/board/topic/57784-request-dota-runes/page__fromsearch__1
guild_vs1,1,1,5	script	Runes	757,1,1,{
dispbottom "Walkthrough to get Runes.";
end;

OnTouch:
	switch( .runes ){
		case 0:
			// 2x ATK / MATK
			sc_start SC_INCATKRATE,( .duration * 1000 ),100;
			sc_start SC_INCMATKRATE,( .duration * 1000 ),100;
			break;
		case 1:
			// Invisible
			skill "AS_CLOAKING",10,1;
			sc_start SC_CLOAKING,( .duration * 1000 ),10;
			break;
		case 2:
			// Restore
			sc_start4 SC_REGENERATION,( .duration * 1000 ),-10,1,0,0;
			break;
		case 3:
			// Clone
			getmapxy( .@map$,.@x,.@y,0,strcharinfo(0) );
			clone .@map$,.@X,.@y,"",getcharid(0),getcharid(0),"",1,.duration;
			clone .@map$,.@X,.@y,"",getcharid(0),getcharid(0),"",1,.duration;
			break;
		case 4:
			// Haste
			sc_start SC_SpeedUp1,( .duration * 1000 ),0;
			break;
		default: end;
	}
	announce "[Rune]  "+strcharinfo(0)+" gained "+.name$[.runes]+".",bc_self,0x00FF00;
	hideonnpc .npc_name$;
	//delwaitingroom;
	.runes = -1;
	attachnpctimer;
	initnpctimer;
	specialeffect2 EF_PROVIDENCE;
	end;
	
OnTimer30000:
	sc_end SC_CLOAKING;
	sc_end SC_REGENERATION;
	end;
OnTimer60000:
	stopnpctimer;
	callsub( OnAssign );
	end;

OnAssign:
	.random = rand( .coordinate_size );
	movenpc .npc_name$,.CoordinateX[ .random ],.CoordinateY[ .random ];
	hideoffnpc .npc_name$;
	if( .runes < 0 ) 
		.runes = rand( .rune_size );
		while(.last_runes == .runes) { // prevent the same rune from spawning
			.runes = rand( .rune_size );
		}
		.last_rune = .runes;
	if( .npc_sprite[.runes] )
		setnpcdisplay( .npc_name$,.name$[.runes],.npc_sprite[.runes] );
	//delwaitingroom;
	//waitingroom "   "+.name$[.runes],0;
	return;

OnInit:
	// Runes Duration in Seconds
	set .duration,30;
	set .runes, -1;
	set .last_runes, -1;

	// Name of Each Runes.
	setarray .name$[0],
		"Double Damage",	//	2 x ATK Rate
		"Invisibility",		//	Cloaking
		"Regeneration",		//	HP / SP Regeneration
		"Illusion",		//	Create 2 Clones
		"Haste";	//	Improve Movement Speed

	// npc sprite switching if any.
	setarray .npc_sprite,
		1908,
		1907,
		1908,
		1907,
		1908;
		
	// Random Coordinate where NPC will Shown Again
	setarray .CoordinateX[0],27,49,73,49,69,69,30,30;
	setarray .CoordinateY[0],49,73,49,25,69,30,30,69;
	
	.rune_size = getarraysize( .name$ );
	.coordinate_size = getarraysize( .CoordinateX );
	.npc_name$ = strnpcinfo(0);
	callsub( OnAssign );
	end;
}

// KoE Entrance
prontera,156,195,4	script	The King#KoE	58,{
    mes "[The King]";
	if ( !getcharid(2) ) {
		mes "You must have a guild to participate in the ^FF0000King of Emperium Hill Tournament^000000.";
		close;
	}
    mes "Hello.";
    mes "Would you like to participate in the ^FF0000King of Emperium Hill Tournament^000000?";
	if ( select ( "Yes", "No" ) == 2 ) close;
    switch( rand(1,4) ){
        case 1:	warp "guild_vs1", 50, 88; end;
        case 2:	warp "guild_vs1", 88, 50; end;
        case 3:	warp "guild_vs1", 50, 11; end;
        case 4:	warp "guild_vs1", 11, 50; end;
	}
}

// KoE Exit
guild_vs1,49,56,5	script	Exit#KoE	51,{
	mes "[Exit]";
	mes "See ya.";
	close2;
	warp "Save",0,0;
	if ( getcharid(2) == $koegid )
		getitem 677, 3; // configure prize here
	end;
}

// Flags
guild_vs1,49,38,4	script	King of Emperium Hill#1::koe_flag	722,{
	if ( !$koegid ) end;
	mes "[King of Emperium Hill]";
	mes "The Current King of Emperium Hill is the ["+ getguildname($koegid) +"] guild.";
	close;
OnInit: // Uncomment this line to make the emblem stay after @reloadscript
OnRevKoE:
	flagemblem $koegid;
	end;
}
guild_vs1,61,49,6	duplicate(koe_flag)	King of Emperium Hill#2	722
guild_vs1,38,49,2	duplicate(koe_flag)	King of Emperium Hill#3	722
guild_vs1,49,61,0	duplicate(koe_flag)	King of Emperium Hill#4	722

guild_vs1	mapflag	nobranch
guild_vs1	mapflag	nomemo
guild_vs1	mapflag	nopenalty
guild_vs1	mapflag	noreturn
guild_vs1	mapflag	nosave	SavePoint
guild_vs1	mapflag	noteleport
guild_vs1	mapflag	gvg_noparty
guild_vs1	mapflag	nowarp
guild_vs1	mapflag	nowarpto
guild_vs1	mapflag	guildlock
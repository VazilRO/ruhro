//===== rAthena Script ======================================= 
//= Man-Hunt Event
//===== By: ================================================== 
//= ????, Originally from EmperiumRO
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Man-Hunt Event
//===== Additional Comments: ================================= 
//= 1.0 First Version. Imported from EmperiumRO script.
//= 1.1 Polished the script and modified the controls [Poutine]
//============================================================

//====================== Main Control =======================
-	script	ManHuntHandler	-1,{
	OnMHAdmin: // Admin Controls
		if (compare(.@atcmd_parameters$,"on")) {
			goto OnStartManHunt;
		} else if (compare(.@atcmd_parameters$,"off")) {
			set $@MHStatus, 0;
			disablenpc "Man-Hunt Event";
			deletearray $@ManHunt[0],getarraysize($@ManHunt);
			deletearray $@ManHuntN$[0],getarraysize($@ManHuntN$);	
			announce "A GM has ended the Man Hunt Event",0;
			mapwarp $@MHMap$,"prontera",156,191;
			stopnpctimer;
		} else {
			dispbottom "type - '@manhunt on' to start the event";
			dispbottom "type - '@manhunt off' to end the event";
		}
		end;	
	
	OnMinute00:
		if(gettime(3) == 0 || gettime(3) == 8 || gettime(3) == 16) {
			goto OnStartManHunt;
		} else {
			disablenpc "Man-Hunt Event";
		}
		end;
		
	OnStartManHunt:
		enablenpc "Man-Hunt Event";
		set $@MHStatus, 1;
		announce "Man-Hunt Event Warper will be closed in 3 minutes. Please visit the middle of Prontera.",0;
		set $@oMH, 0; // dont touch this
		setnpctimer 0;
		initnpctimer;
		end;
		
	OnTimer60000:
		if (.HuntedTimer == 0) {
			announce "Man-Hunt Event Warper will be closed in 2 minutes. Please visit the middle of Prontera.",0;
		}
		end;

	OnTimer120000:
		if (.HuntedTimer == 0) {
			announce "Man-Hunt Event Warper will be closed in 60 Seconds. Please visit the middle of Prontera.",0;
		}
		end;
			
	OnTimer180000:
		if (.HuntedTimer == 0) {
			disablenpc "Man-Hunt Event";
			announce "Man-Hunt Event warper is now closed.",0;
			sleep2 2000;
			if (getmapusers($@MHMap$) < .MHPlayer) goto MHPlayerLack;
			mapannounce $@MHMap$,"The goal of this event is to kill the target within 30 seconds.",0;
			sleep2 4000;
			mapannounce $@MHMap$,"The one who manages to kill the target will gain 1 RedditRO Coin.",0;
			sleep2 4000;
			mapannounce $@MHMap$,"But if the target survives, the target will gain 3 RedditRO Coins.",0;
			sleep2 4000;
			mapannounce $@MHMap$,"Points can be exchanged for RedditRO Coins at the Event NPC.",0;
			sleep2 4000;
			mapannounce $@MHMap$,"Enough explaining, let's go on with the Man-Hunt Event!",0;
			atcommand "@doommap";
			atcommand "@raisemap";
			set .MStart,1;
			stopnpctimer;
			set .MRounds, 1;
			goto ManHuntStart;
		}	
		end;
			
	OnTimer30000:
		if (.HuntedTimer) {
			mapannounce $@MHMap$,"The Target, "+$@ManHuntN$[.Target]+", managed to survive",0;
			for (.@i = 0; .@i < getarraysize($@ManHunt); .@i++) {
				if ($@ManHuntKillable[.@i] == 1) {	
					atcommand "#killable "+$@ManHuntN$[.@i]+"";
					set $@ManHuntKillable[.@i], 0;
				}
				atcommand "#size \""+$@ManHuntN$[.@i]+"\" 0";			
			}
			if(attachrid($@ManHunt[.Target])) {
				if(strcharinfo(3) == $@MHMap$) {
					getitem .MHReward, .MSurvive; // Reward RedditRO Coins directly
				} else {
					dispbottom "Shame on you for leaving Manhunt!";
				}
				detachrid;					
			}
			sleep2 2000;
			set .MRounds, .MRounds + 1;
			stopnpctimer;
			goto MHNextRound;
		}
		end;
	
	GetValidTarget:
		set .valTarg, rand(0, (getarraysize($@ManHunt) - 1));
		while(true) {
			if ($@oMH < .MHPlayer || getmapusers($@MHMap$) < .MHPlayer) goto MHPlayerLack;
			if(attachrid($@ManHunt[.valTarg])) {
				if(strcharinfo(3) != $@MHMap$) {
					detachrid;			
					set .valTarg, rand(0, (getarraysize($@ManHunt) - 1));
				} else {
					detachrid;
					return .valTarg;
				}
			}
		}

			
	ManHuntStart:
		if ($@oMH < .MHPlayer || getmapusers($@MHMap$) < .MHPlayer) goto MHPlayerLack;
		set .Target, callSub(GetValidTarget);
		mapannounce $@MHMap$,"[Round "+.MRounds+"] Our target is "+$@ManHuntN$[.Target]+"!",0;
		sleep2 2000;
		setnpctimer 0;
		set .HuntedTimer, 1;
		mapannounce $@MHMap$,"Ready...?",0;
		sleep2 1000;
		mapannounce $@MHMap$,"3",0;
		sleep2 1000;
		mapannounce $@MHMap$,"2",0;
		sleep2 1000;
		mapannounce $@MHMap$,"1",0;
		sleep2 1000;
		mapannounce $@MHMap$,"Man-Hunt Start!",0;
		atcommand "#size \""+$@ManHuntN$[.Target]+"\" 2";
		if ($@ManHuntKillable[.Target] == 0) {	
			atcommand "#killable "+$@ManHuntN$[.Target]+"";
			set $@ManHuntKillable[.Target], 1;
		}
		initnpctimer;
		end;
			
	OnPCDieEvent: //in victim's scope
		if (strcharinfo(3) == $@MHMap$) {
			if( getcharid(3) == $@ManHunt[.Target] ) {
				if(attachrid(killerrid)) {
					mapannounce $@MHMap$,""+strcharinfo(0)+" has killed the target",0;
					for (.@i = 0; .@i < getarraysize($@ManHunt); .@i++) {
						if ($@ManHuntKillable[.@i] == 1) {	
							atcommand "#killable "+$@ManHuntN$[.@i]+"";
							set $@ManHuntKillable[.@i], 0;
						}
						atcommand "#size \""+$@ManHuntN$[.@i]+"\" 0";			
					}
					getitem .MHReward, .MKill; // Reward the RedditRO Coins directly
					stopnpctimer;
					detachrid;
					sleep2 3000;
					atcommand "#size \""+$@ManHuntN$[.Target]+"\" 0";
					atcommand "@raisemap";
					set .MRounds, .MRounds + 1;
					goto MHNextRound;
				}
			}
		}
		end;
		
	MHPlayerLack:
		for (.@i = 0; .@i < getarraysize($@ManHunt); .@i++) {
			if ($@ManHuntKillable[.@i] == 1) {	
				atcommand "#killable "+$@ManHuntN$[.@i]+"";
				set $@ManHuntKillable[.@i], 0;
			}
			atcommand "#size \""+$@ManHuntN$[.@i]+"\" 0";			
		}
		mapannounce $@MHMap$,"Failed to start Man-Hunt Event due to insufficient Players",0;
		sleep2 2000;
		mapannounce $@MHMap$,"The minimum player required is "+.MHPlayer+" players",0;
		sleep2 2000;
		mapannounce $@MHMap$,"All players will be warped to prontera in 10 seconds.",0;
		sleep2 10000;
		mapwarp $@MHMap$,"prontera",156,191;
		set $@MHStatus, 0;
		set .HuntedTimer, 0;
		set .Target, 0;
		set .MStart, 0;
		deletearray $@ManHunt[0],getarraysize($@ManHunt);
		deletearray $@ManHuntN$[0],getarraysize($@ManHuntN$);
		end;
			
		
	MHNextRound:
		if (getmapusers($@MHMap$) < .MHPlayer) goto MHPlayerLack;
		if (.MRounds > .MRoundsMax) {
			sleep2 2000;
			announce "Man-Hunt Event ended.",0;
			sleep2 2000;
			set $@MHStatus, 0;
			set .HuntedTimer, 0;
			for (.@i = 0; .@i < getarraysize($@ManHunt); .@i++) {
				if ($@ManHuntKillable[.@i] == 1) {	
					atcommand "#killable "+$@ManHuntN$[.@i]+"";
					set $@ManHuntKillable[.@i], 0;
				}
				atcommand "#size \""+$@ManHuntN$[.@i]+"\" 0";			
			}
			deletearray $@ManHunt[0],getarraysize($@ManHunt);
			deletearray $@ManHuntN$[0],getarraysize($@ManHuntN$);
			mapannounce $@MHMap$,"All Players will be warped to prontera in 10 seconds.",0;
			sleep2 10000;
			set .Target, 0;
			set .MStart, 0;
			mapwarp $@MHMap$,"prontera",156,191;
			end;
		} else {
			sleep2 5000;
			mapannounce $@MHMap$,"Next Round in 15 seconds.",0;
			sleep2 15000;
			goto ManHuntStart;
		}
		end;

	OnPCLogOutEvent:
		if ($@MHStatus)
			if (getcharid(3) == $@ManHunt[.Target])
			{
				stopnpctimer;
				mapannounce $@MHMap$,"The target has logged out of this game. This round will be nulled.",0;
				set .MRounds, .MRounds + 1;
				goto MHNextRound;
			}
		end;
			
	OnInit:
		//start of config
		set $@MHMap$, "guild_vs3"; 		// event map
		set .MHReward, 677; 			// reward
		set $@oMH, 0; 					// dont touch this
		set .MRoundsMax, 10; 			// number of rounds
		set .MHPlayer, 1; 				// minimum players to start
		set .MSurvive, 3; 				// Two points for survivor
		set .MKill, 1; 					// One point for killer
		
		disablenpc "Man-Hunt Event";
		setarray $@ManHunt[0],0;
		setarray $@ManHuntN$[0],"";
		setarray $@ManHuntKillable[0], "";
		bindatcmd("manhunt","ManHuntHandler::OnMHAdmin",11,11);	
		end;	
}

//======================== Warper ============================
prontera,156,179,4	script	Man-Hunt Event	831,{

	mes "[Man Hunt Event]";
	if($@MHStatus)
	{
		mes "Hi " + strcharinfo(0) + "!";
		mes "Would you like to join the ^00ff00Man-Hunt Event^000000?";
		if(getgmlevel() > 10) {
			menu "Join as GM",manhuntgm,
				 "Join Event",manhuntplay;
		} else {
			menu "Join Event",manhuntplay;
		}
		
		manhuntgm: // warp GM without adding to the array
			warp $@MHMap$,50,50;
			close;
		manhuntplay:
			for(.@i = 0; .@i < $@oMH; .@i++) { // avoid duplicates
				if($@ManHunt[.@i] == getcharid(3)) {
					warp $@MHMap$,50,50;
					close;
				}	
			}
			set $@ManHunt[$@oMH], getcharid(3);
			set $@ManHuntN$[$@oMH], strcharinfo(0);
			set $@ManHuntKillable[$@oMH], 0;
			set $@oMH, $@oMH + 1;

			if(ismounting()) {
				setmounting();
			}
			warp $@MHMap$,50,50;
			close;
		
	}
	close;
}

guild_vs3	mapflag	restricted	6
//===== rAthena Script ======================================= 
//= Man-Hunt Event
//===== By: ================================================== 
//= ????, Originally from EmperiumRO
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Man-Hunt Event
//===== Additional Comments: ================================= 
//= 1.0 First Version. Imported from EmperiumRO script.
//= 1.1 Polished the script and modified the controls [Poutine]
//============================================================

//====================== Main Control =======================
-	script	ManHuntHandler	-1,{
	OnMHAdmin: // Admin Controls
		if (compare(.@atcmd_parameters$,"on")) {
			goto OnStartManHunt;
		} else if (compare(.@atcmd_parameters$,"off")) {
			set $@MHStatus, 0;
			hideonnpc "Man-Hunt Event";
			deletearray $@ManHunt[0],getarraysize($@ManHunt);
			deletearray $@ManHuntN$[0],getarraysize($@ManHuntN$);	
			announce "A GM has ended the Man Hunt Event",0;
			stopnpctimer;
		} else {
			dispbottom "type - '@manhunt on' to start the event";
			dispbottom "type - '@manhunt off' to end the event";
		}
		end;	
	
	OnMinute00:
		if(gettime(3) == 0 || gettime(3) == 8 || gettime(3) == 16) {
			goto OnStartManHunt;
		} else {
			hideonnpc "Man-Hunt Event";
		}
		end;
		
	OnStartManHunt:
		hideoffnpc "Man-Hunt Event";
		set $@MHStatus, 1;
		announce "Man-Hunt Event Warper will be closed in 3 minutes",0;
		set $@oMH, 0; // dont touch this
		setnpctimer 0;
		initnpctimer;
		end;
		
	OnTimer60000:
		announce "Man-Hunt Event Warper will be closed in 2 minutes",0;
		end;

	OnTimer120000:
		announce "Man-Hunt Event Warper will be closed in 60 Seconds",0;
		end;
			
	OnTimer180000:
		hideonnpc "Man-Hunt Event";
		announce "Man-Hunt Event warper is now closed.",0;
		sleep2 2000;
		if (getmapusers($@MHMap$) < .MHPlayer) goto MHPlayerLack;
		mapannounce $@MHMap$,"The goal of this event is to kill the target within 30 seconds.",0;
		sleep2 2000;
		mapannounce $@MHMap$,"The one who manages to kill the target will gain 1 Man Hunt Point.",0;
		sleep2 2000;
		mapannounce $@MHMap$,"But if the target survives, the target will gain 2 Man Hunt Points.",0;
		sleep2 2000;
		mapannounce $@MHMap$,"Points can be exchanged for Platinum Coins at the Event NPC.",0;
		sleep2 2000;
		mapannounce $@MHMap$,"Enough explaining, let's do the Man-Hunt Event!",0;
		sleep2 2000;
		mapannounce $@MHMap$,"Man-Hunt Event start!",0;
		atcommand "@doommap";
		atcommand "@raisemap";
		set .MStart,1;
		stopnpctimer;
		set .MRounds, 1;
		goto ManHuntStart;
		end;
			
	OnTimer30000:
		if (.HuntedTimer) {
			mapannounce $@MHMap$,"The Target, "+$@ManHuntN$[.Target]+", managed to survive",0;
			atcommand "#killable "+$@ManHuntN$[.Target]+"";
			atcommand "#size \""+$@ManHuntN$[.Target]+"\" 0";
			sleep2 1000;
			if(attachrid($@ManHunt[.Target])) {
				getitem .MHReward, .MSurvive; // Reward Platinum Coins directly
				detachrid;					
			}
			set .MRounds, .MRounds + 1;
			stopnpctimer;
			goto MHNextRound;
		}
		end;
			
	ManHuntStart:
		if (getmapusers($@MHMap$) < .MHPlayer) goto MHPlayerLack;
		set .Target, rand(0,getarraysize($@ManHunt)-1);
		mapannounce $@MHMap$,"[Round "+.MRounds+"] Our target is "+$@ManHuntN$[.Target]+"!",0;
		atcommand "#killable "+$@ManHuntN$[.Target]+"";
		atcommand "#size \""+$@ManHuntN$[.Target]+"\" 2";
		setnpctimer 0;
		set .HuntedTimer, 1;
		initnpctimer;
		end;
			
	OnPCDieEvent: //in victim's scope
		if (strcharinfo(3) == $@MHMap$) {
			if( getcharid(3) == $@ManHunt[.Target] ) {
				if(attachrid(killerrid)) {
					mapannounce $@MHMap$,""+strcharinfo(0)+" has killed the target",0;
					atcommand "#killable "+$@ManHuntN$[.Target]+"";
					atcommand "#size \""+$@ManHuntN$[.Target]+"\" 0";
					getitem .MHReward, .MKill; // Reward the Platinum Coins directly
					stopnpctimer;
					detachrid;
					sleep2 500;
					atcommand "#alive "+$@ManHuntN$[.Target]+"";
					goto MHNextRound;
				}
			}
		}
		end;
		
	MHPlayerLack:
		mapannounce $@MHMap$,"Failed to start Man-Hunt Event due to insufficient Players",0;
		sleep2 2000;
		mapannounce $@MHMap$,"The minimum player required is "+.MHPlayer+" players",0;
		sleep2 2000;
		mapannounce $@MHMap$,"All players will be warped to prontera in 2 seconds.",0;
		sleep2 2000;
		mapwarp $@MHMap$,"prontera",150,150;
		set $@MHStatus, 0;
		set .HuntedTimer, 0;
		set .Target, 0;
		set .MStart, 0;
		deletearray $@ManHunt[0],getarraysize($@ManHunt);
		deletearray $@ManHuntN$[0],getarraysize($@ManHuntN$);
		end;
			
		
	MHNextRound:
		atcommand "@raisemap"; // Allow player to participate again
		if (getmapusers($@MHMap$) < .MHPlayer) goto MHPlayerLack;
		if (.MRounds > .MRoundsMax) {
			sleep2 1000;
			announce "Man-Hunt Event ended.",0;
			sleep2 1000;
			set $@MHStatus, 0;
			set .HuntedTimer, 0;
			deletearray $@ManHunt[0],getarraysize($@ManHunt);
			deletearray $@ManHuntN$[0],getarraysize($@ManHuntN$);
			mapannounce $@MHMap$,"All Players will be warped to prontera in 5 seconds.",0;
			sleep2 5000;
			set .Target, 0;
			set .MStart, 0;
			mapwarp $@MHMap$,"prontera",150,150;
			end;
		} else {
			mapannounce $@MHMap$,"Next Round in 10 seconds.",0;
			sleep2 10000;
			goto ManHuntStart;
		}
		end;

	OnPCLogOutEvent:
		if ($@MHStatus)
			if (getcharid(3) == $@ManHunt[.Target])
			{
				mapannounce $@MHMap$,"The target has logged out of this game. This round will be nulled.",0;
				set .MRounds, .MRounds + 1;
				goto MHNextRound;
			}
		end;
			
	OnInit:
		//start of config
		set $@MHMap$, "guild_vs3"; 		// event map
		set .MHReward, 677; 			// reward
		set $@oMH, 0; 					// dont touch this
		set .MRoundsMax, 10; 			// number of rounds
		set .MHPlayer, 3; 				// minimum players to start
		set .MSurvive, 2; 				// Two points for survivor
		set .MKill, 1; 					// One point for killer
		
		hideonnpc "Man-Hunt Event";
		setarray $@ManHunt[0],0;
		setarray $@ManHuntN$[0],"";
		bindatcmd("manhunt","ManHuntHandler::OnMHAdmin",10,99);	
		end;	
}

//======================== Warper ============================
prontera,156,179,4	script	Man-Hunt Event	831,{

	mes "[Man Hunt Event]";
	if($@MHStatus)
	{
		mes "Status: ^00ff00ACTIVE!^000000";
		menu "Join Event",-;
		set $@ManHunt[$@oMH], getcharid(3);
		set $@ManHuntN$[$@oMH], strcharinfo(0);
		set $@oMH, $@oMH + 1;
		warp $@MHMap$,50,50;
	}
	close;
}